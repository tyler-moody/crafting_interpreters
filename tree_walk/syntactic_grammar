# each expression rule matches at its precedence level *or higher*
expression  ->  equality ;

equality    -> comparison ( ("==" | "!=") comparison )* ;

comparison  -> term ( ( ">" | ">=" | "<" | "<=" ) term)* ;

term        -> factor ( ( "-" | "+" ) factor)* ;

factor      -> unary ( ( "/" | "*" ) unary )* ;

unary       ->  ( "-" | "!" ) unary 
            |   primary ;

primary     ->  NUMBER | STRING | "true" | "false" | "nil" 
            |   "(" expression ")" ;



# How to implement the syntax rules:
#   terminal: code to match and consume the token
#   nonterminal: call to that rule's function
#   |: if or switch statement
#   * or +: while or for loop
#   ?: if statement
